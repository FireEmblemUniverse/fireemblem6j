OUTPUT_ARCH(arm)

Img_SystemObjects = 0x081004A4;
Pal_SystemObjects = 0x08100928;
Pal_UnitSprites = 0x08100968;
Pal_UnitSpritesPurple = 0x081009E8;
Pal_SystemText = 0x08100A08;
Pal_TalkText = 0x08100A28;
Pal_GreenTextColors = 0x08100A68;
Img_PhaseChangePlayer = 0x08100A88;
Pal_PhaseChangePlayer = 0x08100F04;
Img_PhaseChangeEnemy = 0x08100F24;
Pal_PhaseChangeEnemy = 0x0810137C;
Img_PhaseChangeOther = 0x0810139C;
Pal_PhaseChangeOther = 0x081017F4;
Img_PhaseChangeSquares = 0x08101814;

Tsa_Unk_08101974 = 0x08101974;
Tsa_Unk_08101A2C = 0x08101A2C;

Tsa_Unk_081022FC = 0x081022FC;

Anim_08102450 = 0x08102450;

Anim_GasTrapVertical = 0x081025A8;
Anim_GasTrapHorizontal = 0x0810284C;
Img_GasTrapVertical = 0x08102AC0;
Img_GasTrapHorizontal = 0x081030A8;
Pal_GasTrap = 0x0810361C;
Pal_ArrowTrap = 0x0810363C;
Img_ArrowTrap = 0x0810365C;
Anim_ArrowTrap = 0x08103934;
Img_FireTrap = 0x08103EF0;
Pal_FireTrap = 0x08104374;
Anim_FireTrap = 0x08104394;
Img_PikeTrap = 0x08104440;
Anim_PikeTrap = 0x081046C0;
Pal_PikeTrap = 0x081048D4;

Img_SandstormParticles = 0x08105034;
Img_SnowstormParticles = 0x081050BC;
Img_FlamesParticles = 0x08105154;
Pal_FlamesParticles = 0x08105194;
Img_CloudWeather = 0x081051D4;
Pal_CloudWeather = 0x081056F0;

Img_TalkBubble = 0x082E4394;
Pal_TalkBubble = 0x082E4630;

Img_TalkBubbleOpeningA = 0x082E45E0;
Img_TalkBubbleOpeningB = 0x082E4590;
Img_TalkBubbleOpeningC = 0x082E453C;
Img_TalkBubbleOpeningD = 0x082E44E0;
Img_TalkBubbleOpeningE = 0x082E443C;

Img_LimitViewSquares = 0x08308B98;
Pal_LimitViewBlue = 0x08308E98;
Pal_LimitViewRed = 0x08308EF8;
Pal_LimitViewGreen = 0x08308F58;

Tm_ChapterIntroMotif = 0x0830A574;
Img_ChapterIntroMotif = 0x08309574;
Pal_ChapterIntroMotif = 0x0830A770;
Img_ChapterIntroFog = 0x0830A790;
Pal_ChapterIntroFog = 0x0830C414;
Img_GameOverText = 0x0830D604;
Pal_GameOverText = 0x0830D93C;
Pal_Unk_0830D95C = 0x0830D95C;
Tsa_Unk_0830D97C = 0x0830D97C;

gMPlayTable             = 0x0839946C;
gSongTable              = 0x083994D8;

ProcScr_PrepScreen          = 0x085C7E44;
ProcScr_Unk_085C8080        = 0x085C8080;
ProcScr_TerrainHealDisplay  = 0x085C8200;
ProcScr_PoisonDamageDisplay = 0x085C8260;
ProcScr_StatusDecayDisplay  = 0x085C82C8;
ProcScr_TrapDamageDisplay   = 0x085C8320;
ProcScr_Unk_085C83C8        = 0x085C83C8;
ProcScr_AiPhase             = 0x085C8444;
ProcScr_BerserkPhase        = 0x085C846C;
ProcScr_Mu                  = 0x086648B4;
ProcScr_Unk_0868AE04        = 0x0868AE04;
ProcScr_Unk_0868B010        = 0x0868B010;
ProcScr_Unk_0868C304        = 0x0868C304;
ProcScr_Unk_08691480        = 0x08691480;
ProcScr_OpeningSequence     = 0x0869150C;

MenuInfo_085C73C0 = 0x085C73C0;
MenuInfo_085C73E4 = 0x085C73E4;
MenuInfo_085C7408 = 0x085C7408;
MenuInfo_085C742C = 0x085C742C;
MenuInfo_085C7450 = 0x085C7450;
MenuInfo_085C7474 = 0x085C7474;
MenuInfo_085C7498 = 0x085C7498;
MenuInfo_085C74BC = 0x085C74BC;
MenuInfo_085C74E0 = 0x085C74E0;
MenuInfo_085C7504 = 0x085C7504;
MenuInfo_085C7528 = 0x085C7528;
MenuInfo_085C754C = 0x085C754C;
MenuInfo_085C7570 = 0x085C7570;
MenuInfo_085C7594 = 0x085C7594;
MenuInfo_085C75B8 = 0x085C75B8;
MenuInfo_085C75DC = 0x085C75DC;
MenuInfo_UnitMenu = 0x085C7600;
MenuInfo_085C7648 = 0x085C7648;
MapSelectInfo_AttackStaff = 0x085C766C;
MapSelectInfo_Barrier = 0x085C768C;
MapSelectInfo_Restore = 0x085C76AC;
MapSelectInfo_Heal = 0x085C76CC;
MapSelectInfo_085C76EC = 0x085C76EC;
MapSelectInfo_085C770C = 0x085C770C;
MapSelectInfo_085C772C = 0x085C772C;
MapSelectInfo_Steal = 0x085C774C;
MapSelectInfo_085C776C = 0x085C776C;
MapSelectInfo_085C778C = 0x085C778C;
MapSelectInfo_Repair = 0x085C77AC;
MapSelectInfo_085C77CC = 0x085C77CC;
MapSelectInfo_085C77EC = 0x085C77EC;
MapSelectInfo_085C780C = 0x085C780C;
MapSelectInfo_085C782C = 0x085C782C;
MapSelectInfo_085C784C = 0x085C784C;
MapSelectInfo_085C786C = 0x085C786C;

PersonInfoTable = 0x086076D0;
JobInfoTable = 0x0860A130;
ItemInfoTable = 0x0860B648;
MoveTable_Unk_0860C912 = 0x0860C912;
TerrainHealAmountTable = 0x0860CBA9;
TerrainHealsStatusTable = 0x0860CBDC;
TerrainNameStringTable = 0x0860D00C;
FaceInfoTable = 0x0866074C;
JList_BowEffectiveness = 0x086615DB;
JList_HeroCrestPromotes = 0x086615E2;
JList_KnightCrestPromotes = 0x086615EA;
JList_OrionBoltPromotes = 0x086615EF;
JList_ElysianWhipPromotes = 0x086615F4;
JList_GuidingRingPromotes = 0x086615F8;
UnitSpriteTable = 0x08662C14;
AffinityBonuses = 0x0866350C;
ChapterAssets = 0x08664398;

gNumMusicPlayers = 9;
gMaxLines = 0;

SECTIONS
{
    . = 0x02000000;
    EWRAM (NOLOAD) :
    ALIGN(4)
    {
        . = 0x000000; /* ??? */

        . = 0x001000; gMapBuf = .; /* map */
        . = 0x001F70; gChapterIntroMotifTmBuf = .; /* bmfx */
        . = 0x002770; gFont_Unk_02002770 = .; /* map-menu */
        . = 0x002788; gTradeMenuText = .; /* trademenu */
        . = 0x0027D8, gWeatherEffect = .; /* bmio */
        . = 0x002AD8; gWeatherGradient = .; /* bmio */

        . = 0x003238; gUnk_Tm_02003238 = .;
        . = 0x003738; gUnk_Tm_02003738 = .;

        . = 0x01F0E8; /* ewram_data */

        . = ALIGN(4); src/hardware.o(ewram_data);
        . = ALIGN(4); src/move.o(ewram_data);
        . = ALIGN(4); src/sound.o(ewram_data);
        . = ALIGN(4); src/proc.o(ewram_data);
        . = ALIGN(4); src/icon.o(ewram_data);
        . = ALIGN(4); src/debug-text.o(ewram_data);
        . = ALIGN(4); src/text.o(ewram_data);
        . = ALIGN(4); src/banim-sprite.o(ewram_data);
        . = ALIGN(4); src/sprite.o(ewram_data);
        . = ALIGN(4); src/face.o(ewram_data);
        . = ALIGN(4); src/msg.o(ewram_data);
        . = ALIGN(4); src/util.o(ewram_data);
        . = ALIGN(4); src/bm.o(ewram_data);
        . = ALIGN(4); src/unit.o(ewram_data);
        . = ALIGN(4); src/map.o(ewram_data);
        . = ALIGN(4); src/mapwork.o(ewram_data);
        . = ALIGN(4); src/target-list.o(ewram_data);
        . = ALIGN(4); src/unitsprite.o(ewram_data);
        . = ALIGN(4); src/battle.o(ewram_data);
        . = ALIGN(4); src/trap.o(ewram_data);
        . = ALIGN(4); src/supply.o(ewram_data);
        . = ALIGN(4); src/unit-rearrange.o(ewram_data);
        . = ALIGN(4); src/arena.o(ewram_data);
        . = ALIGN(4); src/action.o(ewram_data);

       . = 0x039688; gAiDecision = .;
    }

    . = 0x03000000;
    IWRAM (NOLOAD) :
    ALIGN(4)
    {
        /* .bss */
        . = ALIGN(4); src/random.o(.bss);
        . = ALIGN(4); src/hardware.o(.bss);
        . = ALIGN(4); src/oam.o(.bss);
        . = ALIGN(4); src/talk.o(.bss);
        . = ALIGN(4); src/event.o(.bss);
        . = ALIGN(4); src/anim.o(.bss);
        . = ALIGN(4); src/util.o(.bss);
        . = ALIGN(4); src/map.o(.bss);

        . = 0x002158; src/lib/agb-sram.o(.bss);
        . = ALIGN(4); *libgcc.a:dp-bit.o(.bss);
        . = ALIGN(4); *libgcc.a:fp-bit.o(.bss);

        . = ALIGN(4); src/lib/m4a.o(.bss.code);

        /* COMMON */
        . = 0x0026A0; gOamLoPutIt = .;
        . = 0x0026B0; gDispIo = .;
        . = 0x002720; gIrqFuncs = .;
        . = 0x002758; MapFloodCoreRamFunc = .;
        . = 0x00275C; gOamAffinePutId = .;
        . = 0x002760; PutOamHiRamFunc = .;
        . = 0x002764; gOnHBlankA = .;
        . = 0x002770; gOam = .;
        . = 0x002D70; DrawGlyphRamFunc = .;
        . = 0x002D74; gOamHiPutIt = .;
        . = 0x002D78; gOnHBlankB = .;
        . = 0x002D80; RamFuncArea = .;
        . = 0x003780; DecodeStringRamFunc = .;
        . = 0x003784; PutOamLoRamFunc = .;
        . = 0x003788; gOamAffinePutIt = .;
        . = 0x003790; IntrMainRam = .;
        . = 0x003F90; MapFloodCoreStepRamFunc = .;
        . = 0x003FA0; gEventScriptQueueIt = .;
        . = 0x003FB0; gEventScriptQueue = .;
        . = 0x003FD0; gSpriteAllocIt = .;
        . = 0x004000; gFaces = .;
        . = 0x004010; gWorkingMap = .;
        . = 0x004020; gMapFloodSquareBufB = .;
        . = 0x004220; gWorkingMovTable = .;
        . = 0x004260; gStatGainSimUnit = .;
        . = 0x0042B0; gMapFloodSquareBufA = .;
        . = 0x0044B0; gActiveUnit = .;
        . = 0x0044C0; gMapFloodSt = .;
        . = 0x0052B0; gSoundInfo = .;
        . = 0x006260; gMpi_03006260 = .;
        . = 0x0062A0; gMpi_030062A0 = .;
        . = 0x0062E0; gMpi_FightBgm = .;
        . = 0x006320; gMPlayJumpTable = .;
        . = 0x0063B0; gCgbChans = .;
        . = 0x0064B0; gMpi_030064B0 = .;
        . = 0x0064F0; gMpi_MainBgm = .;
        . = 0x006530; gMpi_03006530 = .;
        . = 0x006570; gMpi_03006570 = .;
        . = 0x0065B0; gMPlayMemAccArea = .;
        . = 0x006600; gMpi_03006600 = .;
        . = 0x006640; ReadSramFast = .;
        . = 0x006644; VerifySramFast = .;
    }

    . = 0x08000000;
    ROM :
    ALIGN(4)
    {
        asm/crt0.o(.text);
        asm/arm.o(.text);

        src/main.o(.text);
        src/irq.o(.text);
        src/random.o(.text);
        src/hardware.o(.text);
        src/move.o(.text);
        src/oam.o(.text);
        src/sound.o(.text);
        src/ramfunc.o(.text);
        src/proc.o(.text);
        src/icon.o(.text);
        src/debug-text.o(.text);
        src/text.o(.text);
        src/banim-sprite.o(.text);
        src/sprite.o(.text);
        src/face.o(.text);
        src/talk.o(.text);
        src/event.o(.text);
        src/anim.o(.text);
        src/game-controller.o(.text);
        src/msg.o(.text);
        src/util.o(.text);
        src/bm.o(.text);
        src/item.o(.text);
        src/unit.o(.text);
        src/map.o(.text);
        src/mapwork.o(.text);
        src/debug-menu.o(.text);
        src/player-phase.o(.text);
        src/bmfx.o(.text);
        src/map-menu.o(.text);
        src/faction.o(.text);
        src/gold.o(.text);
        src/target-list.o(.text);
        src/unitsprite.o(.text);
        src/support.o(.text);
        src/item-use.o(.text);
        src/battle.o(.text);
        src/trademenu.o(.text);
        src/trap.o(.text);
        src/item-action.o(.text);
        src/bmio.o(.text);
        src/chapter.o(.text);
        src/supply.o(.text);
        src/unit-rearrange.o(.text);
        src/arena.o(.text);
        src/action.o(.text);

        asm/code.o(.text);

        asm/lib/m4a.o(.text);
        src/lib/m4a.o(.text);
        asm/lib/agb-syscall.o(.text);
        src/lib/agb-sram.o(.text);
        *libgcc.a:_ashldi3.o(.text);
        *libgcc.a:_call_via_rX.o(.text);
        *libgcc.a:_divsi3.o(.text);
        *libgcc.a:_dvmd_tls.o(.text);
        *libgcc.a:_modsi3.o(.text);
        *libgcc.a:_udivsi3.o(.text);
        *libgcc.a:_umodsi3.o(.text);
        *libgcc.a:dp-bit.o(.text);
        *libgcc.a:fp-bit.o(.text);
        *libgcc.a:_lshrdi3.o(.text);
        *libgcc.a:_muldi3.o(.text);
        *libgcc.a:_negdi2.o(.text);
        *libc.a:memcpy.o(.text);
        *libc.a:memset.o(.text);
        *libc.a:strcpy.o(.text);

        asm/code_blx_stub.o(.text);

        /* .rodata */
        . = ALIGN(4); data/rodata_msg.o(.rodata);
        . = ALIGN(4); src/main.o(.rodata);
        . = ALIGN(4); src/irq.o(.rodata);
        . = ALIGN(4); src/random.o(.rodata);
        . = ALIGN(4); src/hardware.o(.rodata);
        . = ALIGN(4); data/rodata_trig.o(.rodata);
        . = ALIGN(4); src/move.o(.rodata);
        . = ALIGN(4); src/oam.o(.rodata);
        . = ALIGN(4); src/sound.o(.rodata);
        . = ALIGN(4); src/ramfunc.o(.rodata);
        . = ALIGN(4); data/rodata_icon.o(.rodata);
        . = ALIGN(4); src/proc.o(.rodata);
        . = ALIGN(4); src/icon.o(.rodata);
        . = ALIGN(4); src/debug-text.o(.rodata);
        . = ALIGN(4); src/text.o(.rodata);
        . = ALIGN(4); src/banim-sprite.o(.rodata);
        . = ALIGN(4); src/sprite.o(.rodata);
        . = ALIGN(4); src/face.o(.rodata);
        . = ALIGN(4); src/talk.o(.rodata);
        . = ALIGN(4); src/event.o(.rodata);
        . = ALIGN(4); src/anim.o(.rodata);
        . = ALIGN(4); src/game-controller.o(.rodata);
        . = ALIGN(4); src/msg.o(.rodata);
        . = ALIGN(4); src/util.o(.rodata);
        . = ALIGN(4); data/rodata_1004A4.o(.rodata);
        . = ALIGN(4); src/item.o(.rodata);
        . = ALIGN(4); src/unit.o(.rodata);
        . = ALIGN(4); src/debug-menu.o(.rodata);
        . = ALIGN(4); src/bmfx.o(.rodata);
        . = ALIGN(4); src/unitsprite.o(.rodata);
        . = ALIGN(4); src/support.o(.rodata);
        . = ALIGN(4); src/battle.o(.rodata);
        . = ALIGN(4); src/trademenu.o(.rodata);
        . = ALIGN(4); data/menu-info.o(.rodata);
        . = ALIGN(4); src/bmio.o(.rodata);
        . = ALIGN(4); src/chapter.o(.rodata);
        . = ALIGN(4); src/arena.o(.rodata);

        . = ALIGN(4); data/rodata.o(.rodata);

        . = ALIGN(4); src/lib/m4a-tables.o(.rodata);
        . = ALIGN(4); data/rodata_lib.o(.rodata);

        /* .data */
        . = ALIGN(4); src/hardware.o(.data);
        . = ALIGN(4); src/sound.o(.data);
        . = ALIGN(4); src/proc.o(.data);
        . = ALIGN(4); data/data_dbgfont.o(.data);
        . = ALIGN(4); src/text.o(.data);
        . = ALIGN(4); data/data_textglyphs.o(.data);
        . = ALIGN(4); src/sprite.o(.data);
        . = ALIGN(4); src/face.o(.data);
        . = ALIGN(4); src/talk.o(.data);
        . = ALIGN(4); src/event.o(.data);
        . = ALIGN(4); src/anim.o(.data);
        . = ALIGN(4); src/game-controller.o(.data);
        . = ALIGN(4); src/util.o(.data);
        . = ALIGN(4); src/bm.o(.data);
        . = ALIGN(4); src/unit.o(.data);
        . = ALIGN(4); src/map.o(.data);
        . = ALIGN(4); src/debug-menu.o(.data);
        . = ALIGN(4); src/player-phase.o(.data);
        . = ALIGN(4); src/bmfx.o(.data);
        . = ALIGN(4); src/map-menu.o(.data);
        . = ALIGN(4); src/unitsprite.o(.data);
        . = ALIGN(4); src/support.o(.data);
        . = ALIGN(4); src/item-use.o(.data);
        . = ALIGN(4); src/battle.o(.data);
        . = ALIGN(4); src/trademenu.o(.data);
        . = ALIGN(4); src/trap.o(.data);
        . = ALIGN(4); src/item-action.o(.data);
        . = ALIGN(4); data/menu-info.o(.data);
        . = ALIGN(4); src/bmio.o(.data);
        . = ALIGN(4); src/chapter.o(.data);
        . = ALIGN(4); src/arena.o(.data);
        . = ALIGN(4); src/action.o(.data);

        . = ALIGN(4); data/data.o(.data);
    } = 0

  /* Stabs debugging sections.  */
  .stab          0 : { *(.stab) }
  .stabstr       0 : { *(.stabstr) }
  .stab.excl     0 : { *(.stab.excl) }
  .stab.exclstr  0 : { *(.stab.exclstr) }
  .stab.index    0 : { *(.stab.index) }
  .stab.indexstr 0 : { *(.stab.indexstr) }
  .comment       0 : { *(.comment) }
  .gnu.build.attributes : { *(.gnu.build.attributes .gnu.build.attributes.*) }
  /* DWARF debug sections.
     Symbols in the DWARF debugging sections are relative to the beginning
     of the section so we begin them at 0.  */
  /* DWARF 1 */
  .debug          0 : { *(.debug) }
  .line           0 : { *(.line) }
  /* GNU DWARF 1 extensions */
  .debug_srcinfo  0 : { *(.debug_srcinfo) }
  .debug_sfnames  0 : { *(.debug_sfnames) }
  /* DWARF 1.1 and DWARF 2 */
  .debug_aranges  0 : { *(.debug_aranges) }
  .debug_pubnames 0 : { *(.debug_pubnames) }
  /* DWARF 2 */
  .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
  .debug_abbrev   0 : { *(.debug_abbrev) }
  .debug_line     0 : { *(.debug_line .debug_line.* .debug_line_end) }
  .debug_frame    0 : { *(.debug_frame) }
  .debug_str      0 : { *(.debug_str) }
  .debug_loc      0 : { *(.debug_loc) }
  .debug_macinfo  0 : { *(.debug_macinfo) }
  /* SGI/MIPS DWARF 2 extensions */
  .debug_weaknames 0 : { *(.debug_weaknames) }
  .debug_funcnames 0 : { *(.debug_funcnames) }
  .debug_typenames 0 : { *(.debug_typenames) }
  .debug_varnames  0 : { *(.debug_varnames) }
  /* DWARF 3 */
  .debug_pubtypes 0 : { *(.debug_pubtypes) }
  .debug_ranges   0 : { *(.debug_ranges) }
  /* DWARF Extension.  */
  .debug_macro    0 : { *(.debug_macro) }
  .debug_addr     0 : { *(.debug_addr) }
  .ARM.attributes 0 : { KEEP (*(.ARM.attributes)) KEEP (*(.gnu.attributes)) }
  .note.gnu.arm.ident 0 : { KEEP (*(.note.gnu.arm.ident)) }

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}
