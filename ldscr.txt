OUTPUT_ARCH(arm)

gMPlayTable             = 0x0839946C;
gSongTable              = 0x083994D8;

gKeySt = 0x0858E578;

gNumMusicPlayers = 9;
gMaxLines = 0;

SECTIONS
{
    . = 0x02000000;
    EWRAM (NOLOAD) :
    ALIGN(4)
    {
        
    }

    . = 0x03000000;
    IWRAM (NOLOAD) :
    ALIGN(4)
    {
        /* .bss */
        . = 0x002158; src/agb_sram.o(.bss);
        . = ALIGN(4); tools/agbcc/lib/libgcc.a:dp-bit.o(.bss);
        . = ALIGN(4); tools/agbcc/lib/libgcc.a:fp-bit.o(.bss);

        /* COMMON */
        . = 0x0022A0; SoundMainRAM_Buffer = .;
        . = 0x002720; gIrqFuncs = .;
        . = 0x003790; IntrMainRam = .;
        . = 0x0052B0; gSoundInfo = .;
        . = 0x006320; gMPlayJumpTable = .;
        . = 0x0063B0; gCgbChans = .;
        . = 0x0065B0; gMPlayMemAccArea = .;
        . = 0x006640; ReadSramFast = .;
        . = 0x006644; VerifySramFast = .;
    }

    . = 0x08000000;
    ROM :
    ALIGN(4)
    {
        asm/crt0.o(.text);
        asm/arm.o(.text);

        src/main.o(.text);
        src/irq.o(.text);

        asm/code.o(.text);

        asm/m4a_1.o(.text);
        src/m4a.o(.text);
        asm/agb_syscall.o(.text);
        src/agb_sram.o(.text);
        tools/agbcc/lib/libgcc.a:_ashldi3.o(.text);
        tools/agbcc/lib/libgcc.a:_call_via_rX.o(.text);
        tools/agbcc/lib/libgcc.a:_divsi3.o(.text);
        tools/agbcc/lib/libgcc.a:_dvmd_tls.o(.text);
        tools/agbcc/lib/libgcc.a:_modsi3.o(.text);
        tools/agbcc/lib/libgcc.a:_udivsi3.o(.text);
        tools/agbcc/lib/libgcc.a:_umodsi3.o(.text);
        tools/agbcc/lib/libgcc.a:dp-bit.o(.text);
        tools/agbcc/lib/libgcc.a:fp-bit.o(.text);
        tools/agbcc/lib/libgcc.a:_lshrdi3.o(.text);
        tools/agbcc/lib/libgcc.a:_muldi3.o(.text);
        tools/agbcc/lib/libgcc.a:_negdi2.o(.text);
        tools/agbcc/lib/libc.a:memcpy.o(.text);
        tools/agbcc/lib/libc.a:memset.o(.text);
        tools/agbcc/lib/libc.a:strcpy.o(.text);

        asm/code_blx_stub.o(.text);

        . = ALIGN(4); data/rodata_msg.o(.rodata);
        . = ALIGN(4); src/main.o(.rodata);
        . = ALIGN(4); src/irq.o(.rodata);
        . = ALIGN(4); data/rodata.o(.rodata);
        . = ALIGN(4); src/m4a_tables.o(.rodata);
        . = ALIGN(4); data/rodata_lib.o(.rodata);

        . = ALIGN(4); data/data.o(.data);
    } = 0

  /* Stabs debugging sections.  */
  .stab          0 : { *(.stab) }
  .stabstr       0 : { *(.stabstr) }
  .stab.excl     0 : { *(.stab.excl) }
  .stab.exclstr  0 : { *(.stab.exclstr) }
  .stab.index    0 : { *(.stab.index) }
  .stab.indexstr 0 : { *(.stab.indexstr) }
  .comment       0 : { *(.comment) }
  .gnu.build.attributes : { *(.gnu.build.attributes .gnu.build.attributes.*) }
  /* DWARF debug sections.
     Symbols in the DWARF debugging sections are relative to the beginning
     of the section so we begin them at 0.  */
  /* DWARF 1 */
  .debug          0 : { *(.debug) }
  .line           0 : { *(.line) }
  /* GNU DWARF 1 extensions */
  .debug_srcinfo  0 : { *(.debug_srcinfo) }
  .debug_sfnames  0 : { *(.debug_sfnames) }
  /* DWARF 1.1 and DWARF 2 */
  .debug_aranges  0 : { *(.debug_aranges) }
  .debug_pubnames 0 : { *(.debug_pubnames) }
  /* DWARF 2 */
  .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
  .debug_abbrev   0 : { *(.debug_abbrev) }
  .debug_line     0 : { *(.debug_line .debug_line.* .debug_line_end) }
  .debug_frame    0 : { *(.debug_frame) }
  .debug_str      0 : { *(.debug_str) }
  .debug_loc      0 : { *(.debug_loc) }
  .debug_macinfo  0 : { *(.debug_macinfo) }
  /* SGI/MIPS DWARF 2 extensions */
  .debug_weaknames 0 : { *(.debug_weaknames) }
  .debug_funcnames 0 : { *(.debug_funcnames) }
  .debug_typenames 0 : { *(.debug_typenames) }
  .debug_varnames  0 : { *(.debug_varnames) }
  /* DWARF 3 */
  .debug_pubtypes 0 : { *(.debug_pubtypes) }
  .debug_ranges   0 : { *(.debug_ranges) }
  /* DWARF Extension.  */
  .debug_macro    0 : { *(.debug_macro) }
  .debug_addr     0 : { *(.debug_addr) }
  .ARM.attributes 0 : { KEEP (*(.ARM.attributes)) KEEP (*(.gnu.attributes)) }
  .note.gnu.arm.ident 0 : { KEEP (*(.note.gnu.arm.ident)) }

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}
